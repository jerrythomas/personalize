#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Jerry Thomas <me@jerrythomas.name>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_clarity_precmd {
 # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_clarity_colors[1]}%b%f%u%c${_prompt_clarity_colors[4]}●%f)"
  else
    branch_format="(${_prompt_clarity_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_clarity_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_clarity_precmd

  _prompt_clarity_colors=(
    "%F{blue}"   # default
    "%F{green}"  # host
    "%F{cyan}"   # user
    "%F{yellow}" # path
    "%F{red}"    # staged
    "%F{green}"
    "%F{yellow}" # unstaged
  )

  # Show root user in red
  if [[ $UID -eq 0 ]]; then
    _prompt_clarity_colors[2]="%F{red}"
  fi

  # Show host in yellow when connected via SSH
  if [[ -n "$SSH_CLIENT"  ||  -n "$SSH2_CLIENT" ]]; then
    _prompt_clarity_colors[2]="%F{yellow}"
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_clarity_colors[1]} %b%f%u%c)"
  local action_format="(${_prompt_clarity_colors[5]} %a%f)"
  local unstaged_format="${_prompt_clarity_colors[7]} ●%f"
  local staged_format="${_prompt_clarity_colors[5]} ●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="
${_prompt_clarity_colors[1]}(%f${_prompt_clarity_colors[3]}%n%f@${_prompt_clarity_colors[2]}%m%f${_prompt_clarity_colors[1]})%f in ${_prompt_clarity_colors[4]}%~%f "'${vcs_info_msg_0_}'"
"'$python_info[virtualenv]'"${_prompt_clarity_colors[1]}❯${_prompt_clarity_colors[4]}❯%f "
  RPROMPT=''
}

prompt_clarity_setup "$@"
